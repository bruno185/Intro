0 REM "AUXMDVE" DEMO
100 PRINT CHR$(4);"PR#3"
110 FOR I = 768 TO 771: READ X: POKE I,X: NEXT
120 DATA 56: REM "SEC"
130 DATA 76,17,195: REM "JMP $C311"
140 HGR : HCDLDR= 3: HPLOT 10,10 TO 150,150
150 HOME : VTAB 22: PRINT TAB(17);"MAIN <---> AUXILIARY MEMDRY TRANSFER DEMO"
160 HTAB 2: VTAB 23
170 PRINT "PRESS ANY KEY TO SAVE THE SCREEN IN AUXMEM: ";: GET A$
180 REM SET UP THE PARAMETERS OF THE MOVE:
190 POKE 60,0: POKE 61 , 32: REM FROM $2000
200 POKE 62,255: POKE 63,63: REM THROUGH $3FFF
210 POKE 66,0: POKE 67,64: REM T0 $4000 <AUX>
220 CALL 768: REM PERFORM THE MOVE
230 HTAB 2: VTAB 23: CALL - 958
240 PRINT "PRESS ANY KEY TO CLEAR THE SCREEN: ";: GET A$: HGR
250 HTAB 2: VTAB 23: CALL - 958
260 PRINT "PRESS ANY KEY TO RESTORE THE SCREEN FROM AUXMEM: ";: GET A$
270 POKE 768,24: REM PUT IN A "CLC"
280 REM SET UP THE PARAMETERS OF THE MOVE:
290 POKE 60,0: POKE 61,64: REM FROM $4000
300 POKE 62,255: POKE 63,95: REM THROUGH $5FFF
310 POKE 66,0: POKE 67,32: REM T0 $2000 <MAIN>
320 CALL 768: REM PERFORM THE MOVE
